{
  package ast

  import "strings"

  func ifaceStr(val interface{}) string {
    str := ""
    for _, seg := range val.([]interface{}) {
      str = str + string(seg.([]byte))
    }

    return str
  }

  func ifaceSequences(val interface{}) []Sequence {
    seq := []Sequence{}
    for _, node := range val.([]interface{}) {
      seq = append(seq, node.(Sequence))
    }

    return seq
  }

  func ifaceNodes(val interface{}) []Node {
    nodes := []Node{}
    for _, node := range val.([]interface{}) {
      nodes = append(nodes, node.(Node))
    }

    return nodes
  }
}

Booklit <- node:Paragraphs !. {
  return node, nil
}

Paragraphs <- CommentSpacing* paragraphs:(p:Paragraph CommentSpacing* { return p, nil })+ {
  return Sequence(ifaceNodes(paragraphs)), nil
}

CommentSpacing <- "\n" / Comment

Paragraph <- lines:(l:Line "\n" { return l, nil })+ {
  return Paragraph(ifaceSequences(lines)), nil
}

Line <- [ \t]* words:(Word)+ {
  return Sequence(ifaceNodes(words)), nil
}

Word <- Comment? val:(String / Invoke / Interpolated) Comment? {
  return val, nil
}

Comment <- [ \t]* "{-" (Comment / !"-}" .)* "-}"

Interpolated <- '{' word:Word? '}' {
  if word == nil {
    return Sequence{}, nil
  } else {
    return word, nil
  }
}

WrappedLine <- firstWord:Word words:(Word / Split)* {
  allWords := append([]interface{}{firstWord}, words.([]interface{})...)
  return Sequence(ifaceNodes(allWords)), nil
}

Split <- "\n" [ \t]* { return String(" "), nil }

String <- str:[^\\{}\n]+ { return String(c.text), nil } / '\\' [\\{}] { return String(c.text[1:]), nil }

VerbatimString <- str:[^\n}]+ {
  return String(c.text), nil
} / '}' !"}}" {
  return String(c.text), nil
}

PreformattedLine <- indent:Indent words:Word* '\n' {
  line := []Node{String(indent.(string))}
  line = append(line, ifaceNodes(words)...)
  return Sequence(line), nil
}

Preformatted <- '\n' lines:PreformattedLine* {
  delete(c.globalStore, "indent-skip")
  return Preformatted(ifaceSequences(lines)), nil
}

VerbatimLine <- indent:Indent words:VerbatimString* '\n' {
  line := []Node{String(indent.(string))}
  line = append(line, ifaceNodes(words)...)
  return Sequence(line), nil
}

Verbatim <- '\n' lines:VerbatimLine* {
  delete(c.globalStore, "indent-skip")
  return Preformatted(ifaceSequences(lines)), nil
}

Invoke <- '\\' name:([a-z-]+) args:(Argument*) {
  return Invoke{
    Function: ifaceStr(name),
    Arguments: ifaceNodes(args),
  }, nil
}

VerbatimArg <- "{{{" node:Verbatim [ \t]* "}}}" {
  return node, nil
}

PreformattedArg <- "{{" node:Preformatted [ \t]* "}}" {
  return node, nil
}

Arg <- '{' node:(WrappedLine / ParaArg)? '}' {
  if node == nil {
    return String(""), nil
  } else {
    return node, nil
  }
}

ParaArg <- paras:Paragraphs [ \t]* {
  return paras, nil
}

Argument <- VerbatimArg / PreformattedArg / Arg

Indent <- [ \t]* {
  skip := len(c.text)

  i, found := c.globalStore["indent-skip"]
  if found {
    skip = i.(int)
  } else {
    c.globalStore["indent-skip"] = skip
  }

  if skip <= len(c.text) {
    return string(c.text[skip:]), nil
  } else {
    return "", nil
  }
}
